buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'hugo'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

def getBranchName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.boardgamegeek"
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 50
        versionName "5.1.1"
        buildConfigField "String", "BRANCH", "\"" + getBranchName() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // applicationIdSuffix ".debug"
            versionNameSuffix '-' + getBranchName()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:palette-v7:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile('com.squareup.retrofit:converter-simplexml:1.9.0') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile 'com.squareup.okhttp:okhttp:2.7.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'com.jakewharton:butterknife:6.1.0'
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.github.chrisbanes.photoview:library:1.2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.mikepenz.aboutlibraries:library:4.5.9@aar') {
        transitive = true;
    }
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.PhilJay:MPAndroidChart:v2.1.6'
    compile 'com.github.lzyzsd.randomcolor:library:1.0.0'
}
